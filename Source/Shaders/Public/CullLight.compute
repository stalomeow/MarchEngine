#pragma kernel CullMain

#define LIGHT_CULLING
#include "Includes/Lighting.hlsl"

[numthreads(8, 8, 1)]
void CullMain(int3 dtid : SV_DispatchThreadID)
{
    if (dtid.x >= _Nums.x || dtid.y >= _Nums.y || dtid.z >= _Nums.z)
    {
        return;
    }

    float4 planes[6];
    GetClusterPlanes(dtid, planes);

    int clusterIndex = GetClusterIndex(dtid);
    int visibleLightIndex = GetClusterVisibleLightIndicesIndex(clusterIndex, 0);
    _NumVisibleLights[clusterIndex] = 0;

    for (int i = 0; i < _Nums.w && _NumVisibleLights[clusterIndex] < NUM_MAX_LIGHT_IN_CLUSTER; i++)
    {
        LightData light = _Lights[i];
        bool visible = true;

        [branch]
        if (light.position.w != 0)
        {
            float4 position = float4(TransformWorldToView(light.position.xyz), 1.0);

            [unroll]
            for (int j = 0; j < 6; j++)
            {
                if (dot(planes[j], position) < -light.params.x)
                {
                    visible = false;
                    break;
                }
            }
        }

        if (visible)
        {
            _NumVisibleLights[clusterIndex]++;
            _VisibleLightIndices[visibleLightIndex] = i;
            visibleLightIndex++;
        }
    }
}
